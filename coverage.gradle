def jacocoExcludes = [
        '**/*$*',
        '**/*Spec.class',
        'com/challenge/backend/BackendApplication**',
        'com/challenge/backend/repository/**',
        'com/challenge/backend/repository/datastore/**',
        'com/challenge/backend/exception/**',
        'com/challenge/backend/aspect/**',
        'com/challenge/backend/configuration/**',
        'com/challenge/backend/controller/dto/**'
]

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: jacocoExcludes)
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: jacocoExcludes)
        }))
    }

    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification